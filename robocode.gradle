plugins {
    id "de.undercouch.download" version "5.3.0"
}

ext {
    ROBOCODE_DIR = '.robocode'
    ROBOCODE_VERSION = '1.9.4.7'
}

sourceSets {
    main {
        java.destinationDirectory.set(file("${ROBOCODE_DIR}/robots"))
    }
}

dependencies {
    compileOnly fileTree("${ROBOCODE_DIR}/libs/") { include "*.jar" }
}


task setup {
    doLast {
        def setupJarName = "robocode-${ROBOCODE_VERSION}-setup.jar"
        def setupJarPath = "${ROBOCODE_DIR}/${setupJarName}"
        def robocodeCoreJarName = "robocode.core-${ROBOCODE_VERSION}.jar"
        def robocodeCoreJarPath = "${ROBOCODE_DIR}/libs/${robocodeCoreJarName}"

        if (!file(robocodeCoreJarPath).exists()) {
            delete ROBOCODE_DIR
            download.run {
                src "https://nchc.dl.sourceforge.net/project/robocode/robocode/${ROBOCODE_VERSION}/${setupJarName}"
                dest setupJarPath
                overwrite true
                quiet false
            }

            javaexec {
                classpath = files(setupJarPath)
                args ROBOCODE_DIR
                args 'silent'
            }
        } else {
            println "TS"
        }
    }
}

compileJava.dependsOn('setup')

task run(type: JavaExec, dependsOn: 'build') {
    workingDir ROBOCODE_DIR
    classpath "${ROBOCODE_DIR}/libs/*"

    mainClass.set('robocode.Robocode')

    jvmArgs "-Xmx512M"
    jvmArgs "-Dsun.io.useCanonCaches=false"
    jvmArgs "-Ddebug=true"
}